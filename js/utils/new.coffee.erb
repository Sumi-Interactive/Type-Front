uuid = ->
  "xxxxxxxx-xxxx-4xxx-yxxx-xxxxxxxxxxxx".replace /[xy]/g, (c) ->
    r = Math.random() * 16 | 0
    v = (if c is "x" then r else (r & 0x3 | 0x8))
    v.toString 16

if Faye?
  channelID = uuid()

  client = new Faye.Client("<%= ENV['FAYE_SERVER_URL'] %>", retry: 5)

  client.on 'transport:down', ->
    # the client is offline
    setConnectionStatus('Disconnected from server, please refresh browser.')

  client.on 'transport:up', ->
    <% unless Rails.env.production? %>
    console.log("#{window.location.origin}/diaries?channel=#{channelID}")
    <% end %>
    # the client is online
    $('#type-body').html HandlebarsTemplates['diaries/new']()

    $('#type-body .panel-body-bg').hide()
    $('#type-body .panel-body-bg').qrcode({
      render: 'image'
      ecLevel: 'L'
      size: 200
      text: "GridDiary:Type:#{window.location.origin}/diaries?channel=#{channelID}"
    })
    $('#type-body .panel-body-bg').fadeIn(450)

  subscription = client.subscribe "/#{channelID}", (message) ->
    success = message.success

    if success
      $.getJSON message.url, (diary)->
        $('#editor').html HandlebarsTemplates['diaries/edit'](diary)
        subscription.cancel()
        window.app.init()
    else
      setConnectionStatus(message.errors)

  # private
  setConnectionStatus = (status) ->
    $('#type .panel-connection-info').remove()
    $('#type .panel-body').append HandlebarsTemplates['diaries/new/connection_problem'](
        message: status
      )
    $('#type .panel-connection-info').hide().fadeIn(450)
    $('#type .tips').fadeOut(250)

